#!/usr/bin/gnuplot

set term pdfcairo enh size 3,2 lw 2
set output "scaling.pdf"

set fit quiet

set title "neighboring N particles" offset 0,-0.5
set xlab "N"
set ylab "t (sec)"
set xtics 1,10,1e8 format "10^{%L}" nomirror
set ytics 1e-8,1e1,1e8 format "10^{%L}" nomirror
set mxtics 5
set mytics 5
set key top left
set yran [1e-4:]

t0=1e-3

f(x) = t0 + f1*x
g(x) = t0 + g1*x*log(x)
h(x) = t0 + h1*x**2

fit f(x) 'scaling.dat' u 1:3 via f1
fit g(x) 'scaling.dat' u 1:3 via g1

set logscale xy

pl 'scaling.dat' u 1:3 w p ls 1 ps .6 not,\
     f(x) t 'N' ls 1,\
     g(x) t "N log(N)" ls 4 dt 1
