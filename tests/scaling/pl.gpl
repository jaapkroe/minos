#!/usr/bin/gnuplot

set term pdfcairo enh size 3,2 lw 2
set output "scaling.pdf"

set fit quiet

set title "neighboring N particles" offset 0,-0.5
set xlab "N"
set ylab "t (sec)"
set xtics 1,10,1e8 format "10^{%L}" nomirror
set ytics 1e-8,1e1,1e8 format "10^{%L}" nomirror
set mxtics 5
set mytics 5
set key top left
set xran [1e4:]
set yran [1e-2:]

t0=0.0+system("sort -nk3 scaling.dat | awk 'NR==1{print $3}' ")

f2=0.001
f(x) = t0 + f1*x + f2*x**2
g(x) = t0 + g1*x*log(x) 
h(x) = t0 + h1*x**2

fit f(x) 'scaling.dat' u 1:3 via f1,f2
fit g(x) 'scaling.dat' u 1:3 via g1
fit h(x) 'scaling.dat' u 1:3 via h1

set logscale xy

pl 'scaling.dat' u 1:3 w p ls 1 ps .5 not,\
     f(x) t 'N' ls 1,\
     g(x) t "N log(N)" ls 4 dt 1,\
     h(x) t "N^2" ls 3 dt 1

set xran [1:]
set yran [1e-3:]
rep
